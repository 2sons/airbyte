/*
Gradle plugin for Java-based Airbyte connectors.
Also facilitates importing and working with the Java CDK.
*/

import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.tasks.testing.Test

class AirbyteJavaConnectorExtension {

    boolean useLocalCdk
    String cdkVersionRequired
    List<String> features = []  // e.g. 'db-sources', 'db-destinations'
    Project project

    AirbyteJavaConnectorExtension(Project project) {
        this.project = project
    }

    void setUseLocalCdk(boolean useLocalCdk) {
        this.useLocalCdk = useLocalCdk
        addCdkDependencies()
    }

    static final List<String> IMPLEMENTATION = [
            'airbyte-commons',
            'airbyte-json-validation',
            'airbyte-commons-cli',
            'airbyte-api',
            'config-models-oss',
            'init-oss',
    ]

    static final List<String> TEST_IMPLEMENTATION = [
            'airbyte-commons',
            'airbyte-json-validation',
            'airbyte-api',
            'config-models-oss',
    ]

    static final List<String> INTEGRATION_TEST_IMPLEMENTATION = [
            'config-models-oss',
            'init-oss',
            'acceptance-test-harness',
    ]

    void addCdkDependencies() {
        // Create a list of CDK submodules to import
        def submoduleNames = ['core']
        features.each { feature ->
            submoduleNames.add(feature)
        }
        project.dependencies {
            if (useLocalCdk) {
                def submoduleProject = {name ->
                    project.project(":airbyte-cdk:java:airbyte-cdk:${name}")
                }
                for (String dep : IMPLEMENTATION) {
                    implementation submoduleProject(dep)
                    testFixturesImplementation submoduleProject(dep)
                }
                for (String testDep : TEST_IMPLEMENTATION) {
                    testImplementation submoduleProject(testDep)
                }
                for (String intTestDep : INTEGRATION_TEST_IMPLEMENTATION) {
                    integrationTestJavaImplementation submoduleProject(intTestDep)
                }
                submoduleNames.each {submoduleName ->
                    // Add the CDK module to the dependencies
                    def cdkModule = submoduleProject(submoduleName)
                    def cdkModuleTestFixtures = testFixtures(cdkModule)
                    implementation cdkModule
                    testFixturesImplementation cdkModule
                    testFixturesImplementation cdkModuleTestFixtures
                    testImplementation cdkModule
                    testImplementation cdkModuleTestFixtures
                    integrationTestJavaImplementation cdkModule
                    integrationTestJavaImplementation cdkModuleTestFixtures
                    performanceTestJavaImplementation cdkModule
                    performanceTestJavaImplementation cdkModuleTestFixtures
                }
            } else
            {
                def submoduleJar = { name ->
                    "io.airbyte.cdk:airbyte-cdk-${name}:${cdkVersionRequired}"
                }
                for (String dep : IMPLEMENTATION) {
                    implementation submoduleJar(dep)
                }
                for (String testDep : TEST_IMPLEMENTATION) {
                    testImplementation submoduleJar(testDep)
                }
                for (String intTestDep : INTEGRATION_TEST_IMPLEMENTATION) {
                    integrationTestJavaImplementation submoduleJar(intTestDep)
                }
                submoduleNames.each {submoduleName ->
                    // Add the CDK module to the dependencies
                    def cdkModule = submoduleJar(submoduleName)
                    def cdkModuleTestFixtures = "${cdkModule}:test-fixtures"
                    implementation cdkModule
                    testImplementation cdkModule
                    testImplementation cdkModuleTestFixtures
                    integrationTestJavaImplementation cdkModule
                    integrationTestJavaImplementation cdkModuleTestFixtures
                    performanceTestJavaImplementation cdkModule
                    performanceTestJavaImplementation cdkModuleTestFixtures
                }
            }
        }
    }
}


class AirbyteJavaConnectorPlugin implements Plugin<Project> {

    @Override
    void apply(Project project) {

        project.plugins.apply('java-test-fixtures')
        project.plugins.apply(AirbyteIntegrationTestJavaPlugin)
        project.plugins.apply(AirbytePerformanceTestJavaPlugin)

        project.configurations {
            testFixturesImplementation.extendsFrom implementation
            testFixturesRuntimeOnly.extendsFrom runtimeOnly
        }

        project.dependencies {
            // Integration and performance tests should automatically
            // have access to the project's own main source sets.
            integrationTestJavaImplementation project
            integrationTestJavaImplementation testFixtures(project)
            performanceTestJavaImplementation project
            performanceTestJavaImplementation testFixtures(project)
        }

        project.extensions.create('airbyteJavaConnector', AirbyteJavaConnectorExtension, project)
    }
}
